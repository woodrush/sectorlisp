((LAMBDA (MAIN GAMELOOP HELPMESSAGE - <= AND CMP PROGN)
   ((LAMBDA () (QUOTE (THE GAME HAS FINISHED.))) (MAIN)))
 (QUOTE (LAMBDA ()
          (PROGN
            (PRINT (QUOTE (LET'S PLAY A NUMBER GUESSING GAME.
                                I'M THINKING OF A CERTAIN NUMBER
                                BETWEEN 1 AND 10.
                                SAY A NUMBER, AND I'LL TELL YOU IF IT'S
                                LESS THAN, GREATER THAN, OR EQUAL TO MY NUMBER.
                                CAN YOU GUESS WHAT NUMBER I'M THINKING OF?)))
            (HELPMESSAGE)
            (GAMELOOP ())
            (PRINT (QUOTE (DO YOU WISH TO PLAY AGAIN? Y/N:)))
            (PRINT (QUOTE >))
            (COND ((EQ (READ) (QUOTE Y))
                   (PROGN (PRINT (QUOTE (ALRIGHT!)))
                          (MAIN)))
                  ((QUOTE T)
                   (PRINT (QUOTE (THANK YOU FOR PLAYING!))))))))
 (QUOTE (LAMBDA (NUMTRIES)
          (CONS (PRINT (QUOTE NUMBER>))
                ((LAMBDA (GUESS ANSWER NUMTRIES)
                   (COND ((ATOM GUESS)
                          (PROGN (HELPMESSAGE)
                                 (GAMELOOP (CDR NUMTRIES))))
                         ((EQ (QUOTE <) (CMP GUESS ANSWER))
                          (PROGN (PRINT (QUOTE (YOUR GUESS IS
                                                LESS THAN MY NUMBER.)))
                                 (GAMELOOP NUMTRIES)))
                         ((EQ (QUOTE >) (CMP GUESS ANSWER))
                          (PROGN (PRINT (QUOTE (YOUR GUESS IS
                                                GREATER THAN MY NUMBER.)))
                                 (GAMELOOP NUMTRIES)))
                         ((QUOTE T)
                          (PROGN (PRINT (QUOTE (THAT'S RIGHT! MY NUMBER IS:)))
                                 (PRINT ANSWER)
                                 (PRINT (QUOTE (YOU GUESSED CORRECTLY!
                                                CONGRATULATIONS!)))
                                 (PRINT (QUOTE (NUMBER OF TRIES:)))
                                 (PRINT NUMTRIES)))))
                 (READ)
                 (QUOTE (* * * * * * *))
                 (CONS (QUOTE *) NUMTRIES)))))
 (QUOTE (LAMBDA ()
          (PRINT (QUOTE (PLEASE INPUT YOUR NUMBER IN UNARY.
                         FOR EXAMPLE, 1 IS (*), 3 IS (* * *), ETC.)))))
 (QUOTE (LAMBDA (N M)
          (COND ((EQ N NIL) NIL)
                ((EQ M NIL) N)
                ((QUOTE T) (- (CDR N) (CDR M))))))
 (QUOTE (LAMBDA (N M) (EQ NIL (- N M))))
 (QUOTE (LAMBDA (X Y) (COND (X Y) ((QUOTE T) NIL))))
 (QUOTE (LAMBDA (N M)
          (COND ((AND (<= N M) (<= M N)) (QUOTE ==))
                ((<= N M) (QUOTE <))
                ((<= M N) (QUOTE >)))))
 (QUOTE (LAMBDA () NIL)))
(* * * * *)
*
(* * * * * * * *)
(* * * * * * *)
